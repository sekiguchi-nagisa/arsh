
source $SCRIPT_DIR/../modules/completion.arsh

function _sudo($ctx : Module, $t : [String], $c : Int) : Candidates? {
    for(var i = 1; $i < $t.size(); $i++) {
        var v = $t[$i]
        case $v {
        $/^[\\]?-.*$/ => continue
        else => if $i < $t.size() - 1 || $c == $t.size() {
                    return {
                        var argv = $t.slice($i)
                        $argv[0] = $argv[0].dequote()
                        PATH=$PATH:/sbin:/usr/sbin:/usr/local/sbin $compDelegate($ctx, $argv, $c - $i)
                    }
                }
        }
    }
    let wordToken = $t.peek()
    if $c < $t.size() && ($wordToken.startsWith('-') || $wordToken.startsWith('\-')) {
        var word = $wordToken.dequote()
        var ret = new Candidates()
        for o in ['-A', '-b', '-E', '-H', '-n', '-P', '-S'] {
            if $o.startsWith($word) {
                $ret.add($o)
            }
        }
        $ret.quote($wordToken)
        return $ret
    }

    var opt = ['-A', 'external']
    var prefix = $c < $t.size() ? $t.peek() : ""
    if $prefix.contains('/') {
        $opt.add('-A').add('exec')
    }
    if $prefix.startsWith('~') {
        $opt.add('-A').add('tilde')
    }
    PATH=$PATH:/sbin:/usr/sbin:/usr/local/sbin complete -q -d -Q $opt -- $prefix
    return $COMPREPLY
}

$compAdd("sudo", $_sudo)