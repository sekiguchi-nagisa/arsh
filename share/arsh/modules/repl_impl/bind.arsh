
# for key bind setting

function _comp_actions(opt: String, prefix: String): Candidates {
    var ret: Candidates
    for a in $LINE_EDIT.actions() {
        if $a.startsWith($prefix) {
            $ret.add($a)
        }
    }
    return $ret
}

function _comp_keycodes(opt: String, prefix: String): Candidates {
    var ret: Candidates
    for k,_ in $LINE_EDIT.bindings() {
        if $k.startsWith($prefix) {
            $ret.add($k.quote())
        }
    }
    return $ret
}

[<CLI(desc: 'Set or display key bindings')>]
type _BindParam() {
    [<Flag(short: 'l', xor: 1, help: "list defined key actions")>]
    var list = $false

    [<Flag(short: 'p', xor: 1, help: 'list key bindings')>]
    var print = $false

    [<Option(short: 'q', xor: 1, placeholder: 'action', comp:$_comp_actions,
        help: 'find key codes that bind to ACTION')>]
    var query : String?

    [<Option(short: 'r', xor: 1, placeholder: 'keycode', comp:$_comp_keycodes,
        help: 'remove key binding to KEYCODE')>]
    var remove : String?

    [<Arg>]
    var keycode : String?

    [<Arg>]
    var action: String?
}

bind(param : _BindParam) {
    if $param.list {
        for a in $LINE_EDIT.actions().sort() {
            printf "%s\n" $a
        }
        return 0
    }
    if $param.print {
        var bindings : [(String, String)]
        for e in $LINE_EDIT.bindings() {
            $bindings.add($e)
        }
        $bindings.sortBy(function(x, y) => $x._0.compare($y._0))
        for e in $bindings {
            printf "\"%s\": %s\n" ${e._0} ${e._1}
        }
        return 0
    }
    if let action = $param.query {
        var codes : [String]
        for k,v in $LINE_EDIT.bindings() {
            if $action == $v {
                $codes.add($k)
            }
        }
        for c in $codes.sort() {
            printf "\"%s\"\n" $c
        }
        return 0
    }
    if let code = $param.remove {
        try {
            $LINE_EDIT.bind($code)
        } catch e {
            echo 1>&2 ${$param.usage($e.message(), $false)}
            return 1
        }
        return 0
    }
    if let keycode = $param.keycode {
        if !$param.action {
            echo 1>&2 ${$param.usage("require ACTION argument after KEYCODE argument", $false)}
            return 1
        }
        var action = $param.action!
        if $action.empty() {
            echo 1>&2 ${$param.usage("ACTION argument must not be empty", $false)}
            return 1
        }
        try {
            $LINE_EDIT.bind($keycode, $action)
        } catch e {
            echo 1>&2 ${$param.usage($e.message(), $false)}
            return 1
        }
    }
    return 0
}

function _comp_abbr(opt: String, prefix: String): Candidates {
    var ret : Candidates
    for k,_ in $LINE_EDIT.abbrs() {
        if $k.startsWith($prefix) {
            $ret.add($k.quote())
        }
    }
    return $ret
}

# for abbreviation setting
[<CLI(desc: "Set or display abbreviation")>]
type _AbbrParam() {
    [<Flag(short: 'l', xor: 1, help: "list defined abbreviations")>]
    var list = $false

    [<Option(short: 'r', xor: 1, placeholder: 'name', comp: $_comp_abbr,
        help: 'remove abbreviation of PATTERN')>]
    var remove : String?

    [<Arg>]
    var pattern : String?

    [<Arg>]
    var expansion: String?
}

abbr(param: _AbbrParam) {
    if $param.list {
        var list:[(String,String)]
        for e in $LINE_EDIT.abbrs() {
            $list.add($e)
        }
        for e in $list.sort() {
            printf "%s=%s\n" ${e._0} ${e._1}
        }
        return 0
    }
    if let name = $param.remove {
        try {
            $LINE_EDIT.abbr($name)
        } catch e {
            echo 1>&2 ${$param.usage($e.message(), $false)}
            return 1
        }
        return 0
    }
    if let name = $param.pattern {
        if !$param.expansion {
            echo 1>&2 ${$param.usage("require EXPANSION argument after PATTERN argument", $false)}
            return 1
        }
        var expansion = $param.expansion!
        if $expansion.empty() {
            echo 1>&2 ${$param.usage("EXPANSION argument must not be empty", $false)}
            return 1
        }
        try {
            $LINE_EDIT.abbr($name, $expansion)
        } catch e {
            echo 1>&2 ${$param.usage($e.message(), $false)}
            return 1
        }
    }
    return 0
}