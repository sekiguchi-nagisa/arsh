#!/usr/bin/env ydsh

# for path manipulation

function basename(s : String) : String {
    if $s.empty() { return $s; }

    # skip last '/'
    var i = $s.size() - 1
    for(; $i > -1 && $s[$i] == '/'; $i--) {}
    if $i == -1 {
        return "/"
    }
    let end = $i + 1

    # find first '/'
    for(; $i > -1 && $s[$i] != '/'; $i--) {}

    return $s.slice($i + 1, $end)
}

function dirname(s : String) : String {
    if $s.empty() { return "."; }

    # skip last '/'
    var i = $s.size() - 1
    for(; $i > -1 && $s[$i] == '/'; $i--) {}
    if $i == -1 {
        return '/'
    }

    # find first '/'
    for(; $i > -1 && $s[$i] != '/'; $i--) {}

    # skip last '/'
    for(; $i > 0 && $s[$i] == '/'; $i--) {}
    $i++

    return $i == 0 ? "." : $s.slice(0, $i)
}

function tilde(s : String) : String! {
    $s.startsWith('~') || return $s

    var b = $s.slice(1)
    var r = "$(echo ~$b 2>> /dev/null)"
    return ($r.empty() || $r == $s) ? new String!() : $r
}

let home = "$(echo ~)"
let user = "$(whoami)"