
# test cases for completion module

source ../../../../share/arsh/modules/completion.arsh

$BASH_COMPLETION_PATH = $SCRIPT_NAME  ## force disable bash completion

# for compdef
## no option
assert compdef 2>&1 | grep 'compdef: require --cmd option'
compdef &> /dev/null
assert $? == 1

## -h
assert compdef -h 2>&1 | grep 'Usage: compdef \[OPTIONS\]'
compdef &> /dev/null -h
assert $? == 0

## --help
assert compdef --help 2>&1 | grep 'Usage: compdef \[OPTIONS\]'
compdef &> /dev/null --help
assert $? == 0

## invalid option
assert compdef --he 2>&1 | grep 'compdef: invalid option: --he'
compdef &> /dev/null --he
assert $? == 2

## error
assert compdef --cmd @@@@@ --short l
assert ! compdef --cmd @@@@@ --short l
assert $? == 1
assert compdef --cmd @@@@@ --short l 2>&1 | grep "compdef: already defined option: \`-l' for \`@@@@@'"

assert compdef --cmd @@@@@ --short ww 2>&1 | grep  "\`--short' option only accept single ascii char: \`ww'"
assert $PIPESTATUS[0] == 1


## short option
compdef --cmd ls --short l
compdef --cmd ls --short q --arg-list 'AAA ABB ABC CCC D$D D;E'

assert diff <(complete 'ls -') <(echo -l && echo -q)
assert diff <(complete 'ls \-\') <(echo '\-\l' && echo '\-\q')
assert diff <(complete 'ls - ') <(complete -A file)
assert diff <(complete 'ls -l') <(echo -l)
assert diff <(complete 'ls -l\') <(echo '-l\')
assert $(complete 'ls -s').empty()
assert diff <(complete 'ls -q') <(echo -q)
assert diff <(complete 'ls -q ') <(echo AAA && echo ABB && echo ABC && echo CCC && echo 'D\$D' && echo 'D\;E')
assert diff <(complete 'ls -q AB') <(echo ABB && echo ABC)
assert diff <(complete 'ls -q \AB\') <(echo '\AB\B' && echo '\AB\C')
assert diff <(complete 'ls -q D\') <(echo 'D\$D' && echo 'D\;E')

compdef --cmd ls --short s --arg-action env

assert diff <(complete 'ls -s') <(echo -s)
assert diff <(complete 'ls -s P') <(complete -A env P)
assert diff <(complete 'ls -s ') <(complete -A env )


assert $(complete 'ps -x').empty()

compdef --cmd ps --short x --arg-cmd 'complete -A var S'
compdef --cmd ps --arg-list '123 456'

assert diff <(complete 'ps -x ') <(complete -A var S)
assert diff <(complete 'ps -x ST') <(complete -A var ST)
assert $(complete 'ps -x A').empty()
assert diff <(complete 'ps ') <(echo 123 && echo 456)
assert diff <(complete 'ps 4') <(echo 456)
assert $(complete 'ps A').empty()


## long option
compdef --cmd ls --long show --arg-cmd 'ls'
compdef --cmd ls --long stop --arg-action signal
compdef --cmd ls --long top
compdef --cmd ls --long log= --arg-list 'error warn info'

assert diff <(complete 'ls -') <(printf -- "%s\n"  --log= --show --stop --top -l -q -s )
assert diff <(complete 'ls \-') <(printf -- "%s\n"  \\--log= \\--show \\--stop \\--top \\-l \\-q \\-s )

assert diff <(complete 'ls --') <(printf -- "%s\n"  --log= --show --stop --top)
assert diff <(complete 'ls \--sh') <(echo '\--show')
assert diff <(complete 'ls --sto') <(echo --stop)
assert diff <(complete 'ls --sto\') <(echo '--sto\p')
assert diff <(complete 'ls --\show ') <(for line in <(ls){ echo ${$line.quote()}; } | sort)
assert diff <(complete 'ls --show') <(echo --show)
assert diff <(complete 'ls --stop ') <(complete -A signal)
assert diff <(complete 'ls -\-stop T') <(complete -A signal T)
assert diff <(complete 'ls --stop=') <(complete -A signal)
assert diff <(complete 'ls --stop=T') <(complete -A signal T)
assert diff <(complete 'ls --lo') <(echo --log=)
assert { complete 'ls --lo' && $COMPREPLY.size() == 1 && ! $COMPREPLY.hasSpace(0); }
assert diff <(complete 'ls --log=') <(printf "%s\n" error info warn)
assert diff <(complete 'ls --log=\') <(printf "%s\n" '\error' '\info' '\warn')
assert diff <(complete 'ls --log=e') <(printf "%s\n" error)
assert diff <(complete 'ls -\-log=\e\') <(printf "%s\n" '\e\rror')
assert diff <(complete 'ls --t') <(echo --top)
assert $(complete 'ls --top=').empty()
assert $(complete 'ls --top=./').empty()

compdef --cmd ps --long help --arg-list 'ccc aaa ddd bbb'
assert diff <(complete 'ps --he') <(echo --help)
assert $(complete 'ps --s').empty()
assert diff <(complete 'ps --help ') <(echo aaa && echo bbb && echo ccc && echo ddd)
assert diff <(complete 'ps \--h\e\lp \') <(echo '\aaa' && echo '\bbb' && echo '\ccc' && echo '\ddd')
assert $(complete 'ps --help 1').empty()

compdef --cmd ls --long out= --arg-action file
compdef --cmd ls --long out\\= --arg-action file
assert diff <(complete 'ls --out=~') <(complete -A file -A tilde '~')
assert diff <(complete 'ls --ou\t=~') <(complete -A file -A tilde '~')
assert { var a = $(complete 'ls --out=\~'); $a.empty() || $a[0].startsWith('\~'); }  # not expand tilde
assert $(complete 'ls --out\=~/').empty()  # not expand tilde
assert ! $(complete 'ls --out\\=~/').empty()
assert $(complete 'ls --out\\\=~/').empty() # not expand tilde

## long option with single dash
compdef --cmd=cut --long-single 'verbose'
compdef --cmd=cut --long-single 'set=' --arg-list 'aa11 aa12 aa21 aa22 bb11'
assert diff <(complete 'cut -') <(printf "%s\n" '-set=' '-verbose')
assert diff <(complete 'cut \-\') <(printf "%s\n" '\-\set=' '\-\verbose')
assert diff <(complete 'cut -v\er\') <(printf "%s\n" '-v\er\bose')
assert diff <(complete 'cut -verbose') <(printf "%s\n" '-verbose')
assert diff <(complete 'cut -set=') <(printf "%s\n" 'aa11' 'aa12' 'aa21' 'aa22' 'bb11')
assert diff <(complete 'cut \-set=') <(printf "%s\n" 'aa11' 'aa12' 'aa21' 'aa22' 'bb11')
assert diff <(complete 'cut -set=\') <(printf "%s\n" '\aa11' '\aa12' '\aa21' '\aa22' '\bb11')
assert diff <(complete 'cut -set=aa') <(printf "%s\n" 'aa11' 'aa12' 'aa21' 'aa22')
assert diff <(complete 'cut -set=\aa1\') <(printf "%s\n" '\aa1\1' '\aa1\2')

## sub-command
source $SCRIPT_DIR/../../_module4test/comp_nest.ds  as nest
assert diff <(complete 'nest mod fff -') <(printf "%s\n" --count= --dump --help --version -d -h -v)
compdef --cmd ${$nest.mod._fullname('fff')!} --long hey --arg-list 'aaa bbb ccc'
assert diff <(complete 'nest mod fff --he') <(echo --hey)
assert diff <(complete 'nest mod fff --hey ') <(printf "%s\n" aaa bbb ccc)
assert diff <(complete 'nest mod fff --hey a') <(printf "%s\n" aaa)

assert $(complete 'nest mod ggg -').empty()
compdef --cmd ${$nest.mod._fullname('ggg')!} --long hey --arg-list 'aaa bbb ccc'
assert diff <(complete 'nest mod ggg --he') <(echo --hey)
assert diff <(complete 'nest mod ggg --hey ') <(printf "%s\n" aaa bbb ccc)
assert diff <(complete 'nest mod ggg --hey c') <(printf "%s\n" ccc)

# alias
$compAlias('diff', 'ps')
assert diff <(complete 'diff --he') <(echo --help)
assert diff <(complete 'diff --help ') <(echo aaa && echo bbb && echo ccc && echo ddd)

# help-based comp
$DYNA_UDCS['bison'] = () {
    cat << 'EOF'
bison [OPTION] ... [FILE] ...

  -a, --all  show all
  -c         command
  --help     show this help message
  -d, --dump=path
EOF
}

echo
complete 'bison -'

assert diff <(complete 'bison -') <(printf -- "%s\n" --all --dump= --help -a -c -d)
assert diff <(complete 'bison \-') <(printf -- "%s\n" '\--all' '\--dump=' '\--help' '\-a' '\-c' '\-d')
assert diff <(complete 'bison --du') <(printf -- "%s\n" --dump=)
assert diff <(complete 'bison -\-du\') <(printf -- "%s\n" '-\-du\mp=')
assert diff <(complete 'bison --dump=') <(complete -A file)
assert diff <(complete 'bison --dump=/') <(complete -A file /)
assert diff <(complete 'bison --dump=/\') <(complete 'echo /\')
assert diff <(complete 'bison --dump=~/') <(complete -A file -A tilde '~/')
assert diff <(complete 'bison -c ') <(complete -A file)
assert diff <(complete 'bison -c ./') <(complete -A file './')
assert diff <(complete 'bison -c ~/') <(complete -A file -A tilde '~/')