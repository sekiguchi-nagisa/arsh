#$test($status = 1)

# try-catch-finally
var flag = $false
function f($a : Boolean): Int {
    $flag = $false
    try {
        if($a) {
            1 / 0
        }
        return 100
    }
    catch($e) {
        assert($e is Error)
        assert($e is ArithmeticError)
        return -100
    }
    finally {
        $flag = $true
    }
}

assert $? == 0
assert($f($true) == -100)
assert($flag)
assert $? == 1

assert($f($false) == 100)
assert($flag)

# try-finally
$flag = $false
var j = 0
for(var i = 0; $i < 10; $i = $i + 1) {
    try {
        if($i == 5) {
            $flag = $true
            break
        }
    } finally {
        $j = $i
    }
}
assert($flag && $j == 5)

$flag = $false
$j = 0
for(var i = 0; $i < 5; $i = $i + 1) {
    $flag = $false
    try {
        if $i % 2 == 0 {
            $flag = $true
            continue;
        }
    } finally {
        $j = 0
    }
    $j = $i
}
assert $j == 0 && $flag


# nested finally
function g() : Int {
    var i = 0
    try {
        try {
            return $i
        } finally {
            assert($i == 0)
            $i = $i + 1
        }
    } finally {
        assert($i == 1)
        $i = $i + 1
    }
}
assert($g() == 0)

$j = 0
try {
    try {
        1 / 0
        assert($false)
    } catch($e : OutOfRangeError) {
        assert($false)
    } finally {
        $j = $j + 1
    }
} catch($e : ArithmeticError) {
    assert($j == 1)
    $j = $j + 1
} finally {
    assert($j == 2)
    $j = $j + 1
}
assert($j == 3)

# throw in finally
function h() {
    try {
        1 / 0   # ArithmeticError
    } finally {
        [12][34] # not ignore previously raised error
        assert($false)
    }
}

try {
    $h()
} catch($e) {
    assert($e is ArithmeticError)
}
