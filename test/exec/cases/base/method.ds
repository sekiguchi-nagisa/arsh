
typedef ANY : Error
typedef INT : ANY
typedef STR : ANY

# override builtin method
function status() : Int for ANY {
  return -($this as Error).status()
}
assert new ANY('', 100).status() == -100
assert new INT('', 88).status() == -88
assert (new INT('', 88) as Error).status() == 88  ## call base type method

function print() for INT {
    echo INT: ${$this.message()}
}

assert "$(new INT('12345').print())" == 'INT: 12345'

# defined in base type
function print() : Void for ANY {
    echo ANY: "${$this.message()}"
}

assert "$(new INT('12345').print())" == 'INT: 12345'
assert "$((new INT('12345') as ANY).print())" == 'ANY: 12345' ## call base type method
assert "$(new ANY("$True").print())" == 'ANY: true'
assert "$(new STR('hello').print())" == 'ANY: hello'

# recursive call
typedef BigInt(a : Int) { let v = $a; }

function _factorial() : Int for BigInt {
    return $this.v == 0 ? 1 : $this.v * new BigInt($this.v - 1)._factorial()
}

function factorial() : Int for BigInt {
    return $this._factorial()
}

assert new BigInt(0).factorial() == 1
assert new BigInt(1).factorial() == 1
assert new BigInt(2).factorial() == 2
assert new BigInt(3).factorial() == 6
assert new BigInt(4).factorial() == 24

# optional arg
function ff(b : Int?, c : Bool?) : String for STR {
    return $this.message() + ($b ?? 0) + ($c ?? $false)
}

assert new STR("@").ff() == '@0false'
assert new STR("_").ff(4) == '_4false'
assert new STR('__').ff(40, $true) == '__40true'