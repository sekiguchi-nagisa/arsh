
# for error

[<CLI(verbose: $true)>]
typedef AAA() {
    [<Option(range: (10,-10), help: "count")>]
    var Call_Limit = 0

    [<Option(choice: ["error", "warn", "info", "debug"], 
      short: 'l', long: "level", opt: $true)>]
    var Level : String?
}

## range
var aa = new AAA()
assert $aa.parse(["--call-limit=10", "rest", "reset2"]) == 1
assert $aa.Call_Limit == 10

$aa = new AAA()
assert $aa.parse(["--call-limit", "-10"]) == 2
assert $aa.Call_Limit == -10

var ex = 34 as Any
$aa = new AAA()
$aa.setName('cmd1')
try { $aa.parse(["--call-limit=11"]); } catch e { $ex = $e;}
assert $ex is CLIError
assert ($ex as CLIError).status() == 1
assert ($ex as CLIError).message() == "cmd1: invalid argument: \`11' for --call-limit option, must be [-10, 10]
Usage: cmd1 [OPTIONS]

Options:
  --call-limit CALL_LIMIT     count
  -l[LEVEL], --level[=LEVEL]
  -h, --help                  show this help message" : ($ex as CLIError).message()

$ex = 234
$aa = new AAA()
$aa.setName('cmd2')
try { $aa.parse(["--call-limit=-11"]); } catch e { $ex = $e;}
assert $ex is CLIError
assert ($ex as CLIError).status() == 1
assert ($ex as CLIError).message() == "cmd2: invalid argument: \`-11' for --call-limit option, must be [-10, 10]
Usage: cmd2 [OPTIONS]

Options:
  --call-limit CALL_LIMIT     count
  -l[LEVEL], --level[=LEVEL]
  -h, --help                  show this help message" : ($ex as CLIError).message()

## choice
$aa = new AAA()
assert $aa.parse(["--level=debug"]) == 1
assert $aa.Level! == "debug"

$aa = new AAA()
$ex = 234
$aa.setName('cmd3')
try { $aa.parse(["-ldebuG"]); } catch e { $ex = $e; }
assert $ex is CLIError
assert ($ex as CLIError).status() == 1
assert ($ex as CLIError).message() == "cmd3: invalid argument: \`debuG' for -l option, must be {error, warn, info, debug}
Usage: cmd3 [OPTIONS]

Options:
  --call-limit CALL_LIMIT     count
  -l[LEVEL], --level[=LEVEL]
  -h, --help                  show this help message" : ($ex as CLIError).message()

## xor
[<CLI>]
typedef BBB() {
  [<Flag(xor: 0)>]
  var aaa = $false

  [<Flag(xor: 0)>]
  var bbb = $false

  [<Option(xor: 63, required: $true)>]
  var count = 0

  [<Option(required: $true, xor: 63, default: $'aaa\x00bbb', opt: $true)>]
  var dir : String?
}
var bb = new BBB()
$bb.setName('cmd4')
$ex = 45
try { $bb.parse(['--count=12', '--aaa', '--dir']) } catch e { $ex = $e; }
assert $ex is CLIError
assert ($ex as CLIError).status() == 1
assert ($ex as CLIError).message() == "cmd4: --dir option is not allowed after --count option
See \`cmd4 --help' for more information."

$ex = 45
try { $bb.parse(['--count=12', '--bbb', '--count=999', '--aaa']) } catch e { $ex = $e; }
assert $ex is CLIError
assert ($ex as CLIError).status() == 1
assert ($ex as CLIError).message() == "cmd4: --aaa option is not allowed after --bbb option
See \`cmd4 --help' for more information."

$bb = new BBB()
$bb.setName('cmd5')
$ex = 45
try { $bb.parse(['--dir=12', '--aaa', '--dir']) } catch e { $ex = $e; }
assert $ex is Int
assert $bb.aaa
assert !$bb.bbb
assert $bb.count == 0
assert $bb.dir! == $'aaa\x00bbb'


# auto-gen option/arg name
[<CLI(verbose: $true)>]
typedef _AAA() {
    [<Option>]
    var camelCase = ""

    [<Option>]
    var Upper0C1Amel0Case = ""
}
assert new _AAA().usage() == "Usage: $0 [OPTIONS]

Options:
  --camel-case CAMEL_CASE
  --upper0-c1-amel0-case UPPER0_C1_AMEL0_CASE
  -h, --help                                   show this help message" : new _AAA().usage()


# multiple flag
[<CLI>]
type CCC() {
  [<Flag(long:"inc")>]
  [<Flag(long:"dec", store:$false)>]
  var count : Int?

    [<Flag(long:"inc2")>]
  [<Flag(long:"dec2", store:$false)>]
  var count2 = 0

  [<Flag(long:"set")>]
    [<Flag(long:"unset", store:$false)>]
  var flag : Bool?
}

var cc = new CCC()
$cc.setName("cmd6")
assert !$cc.flag
assert $cc.parse(["--set", "--unset", "--unset", "--unset", "--set"]) == 5
assert $cc.flag!  # store last flag

assert !$cc.count
assert $cc.parse(["--dec", "--inc", "--dec"]) == 3
assert $cc.count! == -1 # inc/dec flag

assert $cc.parse(["--dec2", "--inc2", "--dec2", "--inc2", "--inc2", "--dec"]) == 6
assert $cc.count! == -2
assert $cc.count2 == 1

## overflow (saturated)
$cc.count = 9223372036854775806
assert $cc.parse(["--dec", "--inc", "--inc", "--inc"]) == 4
assert $cc.count! == 9223372036854775807
$cc.count2 = -9223372036854775807
assert $cc.parse(["--dec2", "--dec2", "--inc2", "--dec2", "--dec2"]) == 5
assert $cc.count2 == -9223372036854775807 - 1


# multiple option
[<CLI>]
type DDD() {
  [<Option>]
  var time: [Int]?

  [<Option>]
  var time2 = [-99]

  [<Option>]
  var action: [String]?

  [<Option>]
  var action2: [String]
}

var dd = new DDD()
$dd.setName('cmd7')
assert ! $dd.time
assert $dd.time2 == [-99]
assert ! $dd.action
assert $dd.action2.empty()

assert $dd.parse(["--time", "12", "--time2", "222", "--time2=234", "--time", "13", "--time=14"]) == 8
assert $dd.time
assert $dd.time! == [12, 13, 14]
assert $dd.time2.size() == 3
assert $dd.time2 == [-99, 222, 234]
assert ! $dd.action
assert $dd.action2.empty()
assert ($dd.time![0] as Any) is Int
assert ($dd.time![1] as Any) is Int
assert ($dd.time![2] as Any) is Int
assert ($dd.time2[0] as Any) is Int
assert ($dd.time2[1] as Any) is Int

assert $dd.parse(["--action=file", "--action2=FILE", "--action2", "DIR", "--action", "host"]) == 6
assert $dd.action
assert $dd.action! == ["file", "host"]
assert $dd.action2 == ["FILE", "DIR"]
assert ($dd.action![0] as Any) is String
assert ($dd.action![1] as Any) is String
assert ($dd.action2[0] as Any) is String
assert ($dd.action2[1] as Any) is String


# env option
[<CLI>]
type EEE() {
  [<Option(short:'D', long:"def")>]
  var def : [String:String]

  [<Option(short:'W', opt:$true)>]
  var warn: [String:String]?
}

var ee = new EEE()
$ee.setName('cmd8')
assert $ee.def.empty()
assert !$ee.warn

assert $ee.parse(["-W", "-DBUILD_TYPE=debug", "-Wextra", "-Wlevel=4=3", 
                  "-DNDEBUG", "-DOSTYPE=",  "-D=", "--def=BUILD_TYPE=release"]) == 8
assert $ee.def.size() == 4
assert $ee.def["BUILD_TYPE"] == "release"
assert $ee.def["NDEBUG"].empty()
assert $ee.def["OSTYPE"] == ""
assert $ee.def[""] == ""
assert $ee.warn
assert $ee.warn!.size() == 3
assert $ee.warn![""].empty()
assert $ee.warn!["extra"].empty()
assert $ee.warn!["level"] == "4=3"

true