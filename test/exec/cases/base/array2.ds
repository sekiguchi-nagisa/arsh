
# for sortWith method

function rev($x : Int, $y : Int) : Boolean {
    return $x >= $y
}

var a = [5,1,3,2,4]
assert $a.sortWith($rev) is [Int]
assert $a[0] == 5
assert $a[1] == 4
assert $a[2] == 3
assert $a[3] == 2
assert $a[4] == 1

function rev2($x : Int?, $y : Int?) : Boolean {
    return $x! >= $y!
}

var b = [4 as Int?, new Int?(), -1 as Int?]
var ex = 2 as Any
try { $b.sortWith($rev2); assert $false; } catch $e { $ex = $e; }
assert $ex is UnwrappingError


var c = [3, -1, 2, 5, 1]

function rev3($x : Int, $y : Int) : Boolean {
    $c.sortWith($rev3)
    return $x < $y
}

$ex = 0
try { $c.sortWith($rev3); assert $false; } catch $e { $ex = $e; }
assert $ex is StackOverflowError

# stable sort
function comp1($x : (Int, String), $y : (Int, String)) : Boolean {
    return $x._0 < $y._0
}

function comp2($x : (Int, String), $y : (Int, String)) : Boolean {
    return $x._1 < $y._1
}

var d = [
    (9, 'b'),
    (4, 'a'),
    (9, 'c'),
    (-1, 'a'),
]
$d.reverse()
$d.sortWith($comp2)
$d.sortWith($comp1)

assert $d[0]._0 == -1
assert $d[0]._1 == 'a'
assert $d[1]._0 == 4
assert $d[1]._1 == 'a'
assert $d[2]._0 == 9
assert $d[2]._1 == 'b'

# modify array size during sortWith
var aa = $(seq 1 100)
$ex = 34
try { $aa.sortWith(function(x,y)=> { $aa.add("ws"); $x < $y; }); }
catch e { $ex = $e;  }
assert ($ex as InvalidOperationError).message() =~ $/array size has been changed during sortWith method/

true