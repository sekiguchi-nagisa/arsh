
# for user-defined completer

function assertArray($e : [String], $a : [String]) {
    assert diff <(for $i in $e {echo $i; }) <(for $i in $a {echo $i; })
}

function assertComp($s : String, $e : [String]) {
    complete $s > /dev/null
    var values : [String]
    let size = $COMPREPLY.size()
    for(var i = 0; $i < $size; $i++) {
        $values.add($COMPREPLY[$i])
    }
    $assertArray($e, $values)
}

function comp($ctx : Module, $words : [String], $cursor : Int) : Candidates {
    $COMPREPLY.add("hello worod!!")
    $words = $cursor == $words.size() ?
                $words.add($cursor as String) : $words
    return new Candidates($words)
}

$COMP_HOOK = $comp

$assertComp('!echo hey', ['echo', 'hey'])
$assertComp('throw echo hey ', ['2', 'echo', 'hey'])
$assertComp('34 | ls hey', ['hey', 'ls'])
$assertComp('ls && !echo hey 2>&1 > hoge sya', ['echo', 'hey', 'sya'])
$assertComp('echo 2>&1 > hoge ', ['1', 'echo'])
$assertComp('assert echo ${34 + 45}hoge "1234" aa', ['"1234"', '${34 + 45}hoge', 'aa', 'echo'])
$assertComp('echo ~/', ['echo','~/'])
$assertComp('echo a:hoge', ['a:hoge', 'echo'])
$assertComp('echo :hoge', [':hoge', 'echo'])

# for user-defined command
var fullName = ""
$COMP_HOOK = function(ctx, words, c) => {
    $fullName = $words[0]
    return new Candidates()
}
assert complete 'hogehoge '
assert $COMPREPLY.size() == 0
assert $fullName == 'hogehoge'  # before user-defined command definition, cannot resolve fullname
assert ! $MODULE._fullname('hogehoge')

hogehoge() {}

assert complete 'hogehoge '
assert $COMPREPLY.size() == 0
assert $fullName.contains($'\0')
assert $fullName == $MODULE._fullname('hogehoge')!  # after user-defined command definition, resolve fullname

# words
{
    var words2: [String]
    var cursor2 = 0
    $COMP_HOOK = function($ctx : Module, $words : [String], $cursor : Int) => {
        $words2 = $words
        $cursor2 = $cursor
        return $none
    }
    complete -q 'ls '
    assert $words2 == ['ls']
    assert $cursor2 == 1

    complete -q 'ls -la'
    assert $words2 == ['ls', '-la']
    assert $cursor2 == 1

    complete -q 'ls -la '
    assert $words2 == ['ls', '-la']
    assert $cursor2 == 2

    complete -q 'ls -la @'
    assert $words2 == ['ls', '-la', '@']
    assert $cursor2 == 2

    complete -q 'ls -a \'
    assert $words2 == ['ls', '-a', '\']
    assert $cursor2 == 2
}


# escape
function comp2($ctx : Module, $words : [String], $cursor : Int) : Candidates {
    return new Candidates(['\23', '&h', ''])
}

$COMP_HOOK = $comp2

$assertComp('echo ', ['&h', '\23'])  # ignore empty candidates

# invalid
$COMP_HOOK = function(m, w, c) => $none
complete -q 'echo ~'
assert $COMPREPLY.size() > 1  # fallback to filename completion

# error
function comp3($ctx : Module, $words : [String], $cursor : Int) : Candidates {
    $words = $words.add(($cursor/0) as String)
    return new Candidates($words)
}

$COMP_HOOK = $comp3

assert "$(complete 'echo ')".empty()   # if throw error from user-dfined completer, cancel completion

var ex = 34 as Any
try { complete 'echo '; assert $false; } catch($e) { $ex = $e; }
assert $ex is ArithmeticError
## assert $? == 1  # if error happened

# pass module context
$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) => {
    complete > /dev/null -m $ctx -A var 'ass'
    return $COMPREPLY
}
$assertComp('echo ', ['assertArray', "assertComp"])

$ex = 45
$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) -> {
    $words.shift()
    $c--
    var line = ""
    for $e in $words {
        if !$line.empty() { $line += " "; }
        $line += $e
    }
    complete -m $ctx $line > /dev/null
    return $COMPREPLY
}
$assertComp('shctl shctl ', ["shctl"])

$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) => {
    complete -m $ctx '{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{ echo '
    return $COMPREPLY
}
 try { $assertComp('shctl ', ["shctl"]); } catch $e { $ex = $e; }
 assert $ex is StackOverflowError


# call method on temporary module object
## fullname
$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) => {
    return new Candidates([$ctx._fullname("")!])
}
$ex = 34
try { $assertComp('echo ', [""]); } catch $e { $ex = $e; }
assert $ex is InvalidOperationError
assert ($ex as InvalidOperationError).message() == "cannot call method on temporary module object"

## scriptName
$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) => {
    return new Candidates([$ctx._scriptName()])
}
$ex = 34
try { $assertComp('echo ', [""]); } catch $e { $ex = $e; }
assert $ex is InvalidOperationError
assert ($ex as InvalidOperationError).message() == "cannot call method on temporary module object"

## scriptDir
$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) => {
    return new Candidates([$ctx._scriptDir()])
}
$ex = 34
try { $assertComp('echo ', [""]); } catch $e { $ex = $e; }
assert $ex is InvalidOperationError
assert ($ex as InvalidOperationError).message() == "cannot call method on temporary module object"

## func
$COMP_HOOK = function($ctx : Module, $words : [String], $c : Int) => {
    $ctx._func("echo hello")()!
    var ret : Candidates
    for a in $words {
        $ret.add($a)
    }
    return $ret
}
$ex = 34
try { $assertComp('echo ', [""]); } catch $e { $ex = $e; }
assert $ex is InvalidOperationError
assert ($ex as InvalidOperationError).message() == "cannot call method within user-defined completer"

## mood
var ccc = new Candidates(['ABCD'])
$COMP_HOOK = function(ctx, words, c) => {
    return $ccc
}
$ex = 34
try {
    for(var i = 0; $i < 2; $i++) {
        complete -- 'ls ';  # never modify callback return value
    }
} catch e { $ex = $e; }
assert $ex is Int
assert $COMPREPLY[0] == 'ABCD'

true