
#$test($status = 0, $out = $'hello\nworld\nAAA\nCONT\n')

# background job
var j = (/bin/sh -c 'echo world' && /bin/sh -c 'echo hello') | sort &
assert ! $j[0]
assert ! $j[1]
assert !$j.in()
assert ! $j.out()
assert $j.size() == 2

var ex = 34 as Any
try { $j[2]; } catch $e { $ex = $e; }
assert $ex is OutOfRangeError

assert $j as String == "%1"
assert $j.wait() == 0


# disowned background job
$j = /bin/sh -c 'echo AAA' &!
assert ! $j[0]
assert ! $j[1]
assert !$j.in()
assert !$j.out()
assert $j.size() == 1
assert $j as String == "%0"
assert $j.wait() == 0


# co-process
$j = coproc { read; echo reply: $REPLY; }
assert $j[0]
assert $j[1]
assert $j.in()
assert $j.out()
assert $j.size() == 1
assert $j.pid(0) > 0

$ex = 34 as Any
try { $j.pid(1); } catch $e { $ex = $e; }
assert $ex is OutOfRangeError

echo hello > $j[0]
assert { read -u $j[1]; $REPLY; } == "reply: hello"

assert $j.wait() == 0
assert ! $j.in()
assert !$j.out()
assert $j.size() == 1


# jobs in child
$j = {
    var j = exit 3 &
    exit ${$j.wait()}
} &
assert $j.wait() == 3


var j2 = true &
$j = {
 var s = $j2.wait()
 exit $s;
} &
assert $j.wait() == 1 : "${$j.wait()}"  # raise system error
assert $j2.wait() == 0


# signal
$j = for(;;) { true; } &
assert $j.poll()
$j.raise(%'STOP')
assert $j.poll()

$j.raise(%'CONT')
assert $j.poll()
$j.raise(%'term')
assert $j.wait() == %'term'.value() + 128
assert !$j.poll()

## coredump
$j = for(;;) { true; } &
$j.raise(%'quit')
assert $j.wait() == %'quit'.value() + 128
assert !$j.poll()
$j.raise(%'term')   # do nothing

# handle SIGCONT
function handle($s : Signal) {
    echo ${$s.name()}
    exit $s
}
$j = { $SIG[%'cont'] = $handle; for(;;) { true; } } &
sleep 1
$j.raise(%'STOP')
$j.raise(%'CONT')
assert $j.wait() == %'cont'.value()


# detach
$j = coproc false
assert $j as String == '%1'
$j.detach()
assert $j as String == '%0'
assert $j.wait() == 1
