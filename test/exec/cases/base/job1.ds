
#$test($status = 0, $out = $'hello\nworld\nAAA\nCONT\n')

# background job
var j = (/bin/sh -c 'echo world' && /bin/sh -c 'echo hello') | sort &
assert ! $j[0]
assert ! $j[1]
assert !$j.in()
assert ! $j.out()
assert $j.size() == 2

var ex = 34 as Any
try { $j[2]; } catch $e { $ex = $e; }
assert $ex is OutOfRangeError

$ex = 3400
try { $j.status(100); } catch $e { $ex = $e; }
assert $ex is OutOfRangeError

assert $j as String == "%1"
assert $j.wait() == 0
assert $j.status(0)! == 0
assert $j.status(1)! == 0
assert $j as String == "%0"  # after termination, jobId will be 0


# disowned background job
$j = /bin/sh -c 'echo AAA && sleep 1' &!
assert $j as String == "%1"
assert ! $j[0]
assert ! $j[1]
assert !$j.in()
assert !$j.out()
assert $j.size() == 1
assert $j.wait() == 0
assert $j as String == "%0"

# co-process
$j = coproc { read; echo reply: $REPLY; }
assert $j[0]
assert $j[1]
assert $j.in()
assert $j.out()
assert $j.size() == 1
assert $j.pid(0)! > 0
assert !$j.status(0)

$ex = 34 as Any
try { $j.pid(1); } catch $e { $ex = $e; }
assert $ex is OutOfRangeError

echo hello >& $j[0]
assert { read -u $j[1]; $REPLY; } == "reply: hello"

assert $j.wait() == 0
assert $j.in()  # in fd is still available
assert $j.out()  # out fd is still available
assert $j.size() == 1
assert $j.status(0)! == 0

# jobs in child
$j = {
    var j = exit 3 &
    exit ${$j.wait()}
} &
assert $j.wait() == 3
assert $j.status(0)! == 3

var j2 = sleep 0.5 &
$j = {
 assert $j2 as String != '%0'
 sleep 0.5
 assert !$j2.pid(0)
 assert !$j2.poll()
 var s = try { $j2.wait(); } catch $ex { assert $ex is SystemError; 100; }
 exit $s;
} &
assert !$j.status(0)
assert $j.wait() == 100 : "${$j.wait()}"  # raise system error
assert $j.status(0)! == 100
assert $j2.wait() == 0


# signal
$j = for(;;) { true; } &
assert $j.poll()
$j.kill($SIGSTOP)
assert $j.poll()

$j.kill($SIGCONT)
assert $j.poll()
$j.kill($SIGTERM)
assert $j.wait() == $SIGTERM.value() + 128
assert $j.status(0)! == $SIGTERM.value() + 128
assert !$j.poll()

## coredump
$j = for(;;) { true; } &
$j.kill($SIGQUIT)
assert $j.wait() == $SIGQUIT.value() + 128
assert !$j.poll()

$ex = 34
try {
    $j.kill($SIGTERM)   # do nothing
} catch e {
    $ex = $e
}
assert ($ex as SystemError).message().startsWith('signal sending failed')

# handle SIGCONT
function handle($s : Signal) {
    echo ${$s.name()}
    exit $s
}
$j = { $SIG['cont'].trap($handle); for(;;) { true; } } &
sleep 1
$j.kill($SIGSTOP)
$j.kill($SIGCONT)
assert $j.wait() == $SIGCONT.value()
assert $j.status(0)! == $SIGCONT.value()

# detach
$j = coproc { sleep 1; false; }
$j.disown()
assert $j as String == '%1'   # after detaching, still maintains valid job id
assert $j.wait() == 1
assert $j as String == '%0'