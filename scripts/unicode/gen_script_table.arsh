#!/usr/bin/env arsh

[<CLI(toplevel:$true)>]
type _Param() {
    [<Option(required: $true, help: "specify header file")>]
    var header = ""
    [<Arg(required:$true, help: "specify output file")>]
    var output = ""
}

var param = new _Param()
$param.parse($@)

let out = $param.output
write() { echo $@ >> $out; }

source ./guniset_helper.arsh

shctl set errraise

## generate script header
let cates : [String]
let cateNames : [String: [String]]
let excludes  = @(Zzzz) # Unknown
{
    auto_gen_header
    echo '#define EACH_UCP_SCRIPT_PRIME(E) \'
    for line in <(guniset enum 'sc') {
        let ss = $line.split(", ")
        let abbr = $ss.shift()
        $cateNames.put($abbr, $ss)
        $excludes.contains($abbr) && continue
        $cates.add($abbr)
        echo "    E($abbr, \"$abbr|${$ss.join('|')}\") \\"
    }
    echo
    echo
    echo '#define EACH_UCP_SCRIPT(E) \'
    echo '    EACH_UCP_SCRIPT_PRIME(E) \'
    let size = $excludes.size()
    for(var i = 0; $i < $size; $i++) {
        var cat = $excludes[$i]
        echo "    E($cat, \"$cat|${$cateNames[$cat].join('|')}\")${$i < $size - 1 ? ' \' : ""}"
    }
} with > ${param.header}


## generate each script set table
auto_gen_header > $out

var sizeMap: [String: OffsetEntry]
var offset = 0
write "static constexpr PROPERTY_SET_RANGE script_set_table_except_Zzzz[] = {"
for cate in $cates {
    var set = $createCodePointSet("sc:$cate", $true)
    write "    /* $cate */"
    $set.emit() with >> $out
    $sizeMap.put($cate, $set.toOffsetEntry($offset))
    $offset += $set.tableSize()
}
write "};"

## generate final table
write 
write "static constexpr PROPERTY_SET_RANGE_TABLE_OFFSET script_set_table_offset_except_Zzzz[] = {"
for cate, s in $sizeMap {
    write "    {${$s.str()}}, // $cate"
}
write "};"
write

## generate each script extension table
$sizeMap.clear()
$offset = 0
write "static constexpr PROPERTY_SET_RANGE scriptx_set_table[] = {"
for cate in $cates {
    var set = $createCodePointSet("scx:$cate", $true)
    $set.empty() && continue
    write "    /* $cate */"
    $set.emit() with >> $out
    $sizeMap.put($cate, $set.toOffsetEntry($offset))
    $offset += $set.tableSize()
}
write "};"
write
write '#define EACH_UCP_SCRIPT_EXTENSION(E) \'
var c = 0;
for cate, s in $sizeMap {
    write "    E($cate, ${$s.str()})${$c < $sizeMap.size() - 1 ? ' \' : ""}"
    $c++
}

echo code generation success