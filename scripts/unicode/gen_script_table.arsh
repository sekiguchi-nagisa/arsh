#!/usr/bin/env arsh

[<CLI(toplevel:$true)>]
type _Param() {
    [<Option(required: $true, help: "specify header file")>]
    var header = ""
    [<Arg(required:$true, help: "specify output file")>]
    var output = ""
}

var param = new _Param()
$param.parse($@)

let out = $param.output
write() { echo $@ >> $out; }

source ./guniset_helper.arsh

shctl set errraise

## generate script header
let cates : [String]
let cateNames : [String: [String]]
let excludes  = @(Zzzz) # Unknown
{
    auto_gen_header
    echo '#define EACH_UCP_SCRIPT_PRIME(E) \'
    for line in <(guniset enum 'sc') {
        let ss = $line.split(", ")
        let abbr = $ss.shift()
        $cateNames.put($abbr, $ss)
        $excludes.contains($abbr) && continue
        $cates.add($abbr)
        echo "    E($abbr, \"$abbr|${$ss.join('|')}\") \\"
    }
    echo
    echo
    echo '#define EACH_UCP_SCRIPT(E) \'
    echo '    EACH_UCP_SCRIPT_PRIME(E) \'
    let size = $excludes.size()
    for(var i = 0; $i < $size; $i++) {
        var cat = $excludes[$i]
        echo "    E($cat, \"$cat|${$cateNames[$cat].join('|')}\")${$i < $size - 1 ? ' \' : ""}"
    }
} with > ${param.header}


## generate each script set table
auto_gen_header > $out

var bmpSizeMap: [String: Int]
for cate in $cates {
    var set = $createCodePointSet("sc:$cate")
    write "static constexpr PROPERTY_SET_RANGE script_set_table_$cate[] = {"
    $set.emit() with >> $out
    write "};"
    $bmpSizeMap.put($cate, $set.bmp.size())
}

## generate final table
write 
write "static constexpr PROPERTY_SET_RANGE_TABLE script_set_table_except_Zzzz[] = {"
for cate, s in $bmpSizeMap {
    write "    {$s, script_set_table_$cate, std::size(script_set_table_$cate)}, // $cate"
}
write "};"

## generate each script extension table
$bmpSizeMap.clear()
for cate in $cates {
    var set = $createCodePointSet("scx:$cate")
    $set.empty() && continue
    write "static constexpr PROPERTY_SET_RANGE scriptx_set_table_$cate[] = {"
    $set.emit() with >> $out
    write "};"
    $bmpSizeMap.put($cate, $set.bmp.size())
}
write
write '#define EACH_UCP_SCRIPT_EXTENSION(E) \'
var c = 0;
for cate, bmpSize in $bmpSizeMap {
    write "    E($cate, $bmpSize)${$c < $bmpSizeMap.size() - 1 ? ' \' : ""}"
    $c++
}

echo code generation success