#!/usr/bin/env arsh

[<CLI(toplevel:$true)>]
type _Param() {
    [<Option(required: $true, help: "specify header file")>]
    var header = ""
    [<Arg(required:$true, help: "specify output file")>]
    var output = ""
}

var param = new _Param()
$param.parse($@)

let out = $param.output
write() { echo $@ >> $out; }

source ./guniset_helper.arsh

shctl set errraise

## generate category header
let excludes = @(LC L M N P S Z Cn C)
let cates : [String]
let cateNames : [String: [String]]
{
    auto_gen_header
    echo '#define EACH_UCP_GENERAL_CATEGORY_PRIME(E) \'
    for line in <(guniset enum 'gc') {
        let ss = $line.split(", ")
        let abbr = $ss.shift()
        $cateNames.put($abbr, $ss)
        $excludes.contains($abbr) && continue
        $cates.add($abbr)
        echo "    E($abbr, \"$abbr|${$ss.join('|')}\") \\"
    }
    echo
    echo
    echo '#define EACH_UCP_GENERAL_CATEGORY(E) \'
    echo '    EACH_UCP_GENERAL_CATEGORY_PRIME(E) \'
    let size = $excludes.size()
    for(var i = 0; $i < $size; $i++) {
        var cat = $excludes[$i]
        echo "    E($cat, \"$cat|${$cateNames[$cat].join('|')}\")${$i < $size - 1 ? ' \' : ""}"
    }
} with > ${param.header}


## generate each category set table
auto_gen_header > $out

var sizeMap: [String: OffsetEntry]
var offset = 0
write "static constexpr PROPERTY_SET_RANGE category_set_table_except_Cn[] = {"
for cate in $cates {
    var set = $createCodePointSet("cat:$cate", $true)
    write "    /* $cate */"
    $set.emit() with >> $out
    $sizeMap.put($cate, $set.toOffsetEntry($offset))
    $offset += $set.tableSize()
}
write "};"

## generate final table
write 
write "static constexpr PROPERTY_SET_RANGE_TABLE_OFFSET category_set_table_offset_except_Cn[] = {"
for cate, s in $sizeMap {
    write "    {${$s.str()}}, // $cate"
}
write "};"

echo code generation success