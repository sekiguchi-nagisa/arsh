#!/usr/bin/env arsh

source ./guniset_helper.arsh

[<CLI(toplevel:$true)>]
type _Param() {
    [<Option(required: $true, help: "specify header file")>]
    var header = ""
    [<Arg(required:$true, help: "specify output file")>]
    var output = ""
}

## helper
let combinedSet = [  # generated from other property combination
    "Alphabetic": "dcp:Lowercase + dcp:Uppercase + cat:Lt + cat:Lm + cat:Lo + cat:Nl + prop:Other_Alphabetic",
    "Assigned": "",
    "Case_Ignorable": "cat:Mn + cat:Me + cat:Cf + cat:Lm + cat:Sk + wbp:MidLetter + wbp:MidNumLet + wbp:Single_Quote",
    "Cased": "dcp:Lowercase + dcp:Uppercase + cat:Lt",
    "Default_Ignorable_Code_Point": 
        "prop:Other_Default_Ignorable_Code_Point + cat:Cf + prop:Variation_Selector - prop:White_Space - U+FFF9..U+FFFB - U+13430..U+1343F - prop:Prepended_Concatenation_Mark",
    "Grapheme_Base": 
        "U+0..U+10FFFF - cat:Cc - cat:Cf - cat:Cs - cat:Co - cat:Cn - cat:Zl - cat:Zp - dcp:Grapheme_Extend",
    "Grapheme_Extend": "cat:Me + cat:Mn + prop:Other_Grapheme_Extend",
    "ID_Continue": 
        "dcp:ID_Start + cat:Mn + cat:Mc + cat:Nd + cat:Pc + prop:Other_ID_Continue - prop:Pattern_Syntax - prop:Pattern_White_Space",
    "ID_Start": "cat:L + cat:Nl + prop:Other_ID_Start - prop:Pattern_Syntax - prop:Pattern_White_Space",
    "Lowercase": 'cat:Ll + prop:Other_Lowercase',
    "Math": "cat:Sm + prop:Other_Math",
    "Uppercase": 'cat:Lu + prop:Other_Uppercase',
    "XID_Continue": "",
    "XID_Start": "",
]

function assertCombinedSet() {
    for k, v in $combinedSet {
        $v.empty() && continue
        assert (diff <(guniset generate "dcp:$k") <(guniset generate $v)) : $k
    }

    # for XID
    assert !$(guniset generate "dcp:ID_Continue - dcp:XID_Continue").empty()
    assert $(guniset generate "dcp:XID_Continue - dcp:ID_Continue").empty()
    assert !$(guniset generate "dcp:ID_Start - dcp:XID_Start").empty()
    assert $(guniset generate "dcp:XID_Start - dcp:ID_Start").empty()
}

type LoneProperty(n: String, a: String, p: String?) {
    let name = $n
    let abbr = $a != "-" ? $a : $none
    let prefix = $p
    let query = case $name {
        "ASCII" => "0..7F"
        "Any" => "0..10FFFF"
        "XID_Continue_Delta" => "dcp:ID_Continue - dcp:XID_Continue"
        "XID_Start_Delta" => "dcp:ID_Start - dcp:XID_Start"
        else => $prefix && !$combinedSet.get($name)  ? $prefix! + ":" + $name : $none
    }
}

function create(args: [[String]]) : [LoneProperty] {
    var ret: [LoneProperty]
    for arg in $args {
        var name = $arg[0]
        var setPrefix = case $arg.get(2) {
            "PropList.txt" => "prop"
            "DerivedCoreProperties.txt" => "dcp"
            "emoji-data.txt" => "emoji"
            "DerivedBinaryProperties.txt" => "dbp"
            "DerivedNormalizationProps.txt" => "dnp"
            "WordBreakProperty.txt" => "wbp"
            else => $none
        }
        $ret.add(new LoneProperty($name, $arg[1], $setPrefix))
    }
    return $ret
}



## target property definition
let defs = $create([
@( ASCII                         -                                       ),
@( ASCII_Hex_Digit               AHex      PropList.txt                  ),
@( Alphabetic                    Alpha     DerivedCoreProperties.txt     ),
@( Any                           -                                       ),
@( Assigned                      -                                       ),
@( Bidi_Control                  Bidi_C    PropList.txt                  ),
@( Bidi_Mirrored                 Bidi_M    DerivedBinaryProperties.txt   ),
@( Case_Ignorable                CI        DerivedCoreProperties.txt     ),
@( Cased                         -         DerivedCoreProperties.txt     ),
@( Changes_When_Casefolded       CWCF      DerivedCoreProperties.txt     ),
@( Changes_When_Casemapped       CWCM      DerivedCoreProperties.txt     ),
@( Changes_When_Lowercased       CWL       DerivedCoreProperties.txt     ),
@( Changes_When_NFKC_Casefolded  CWKCF     DerivedNormalizationProps.txt ),
@( Changes_When_Titlecased       CWT       DerivedCoreProperties.txt     ),
@( Changes_When_Uppercased       CWU       DerivedCoreProperties.txt     ),
@( Dash                          -         PropList.txt                  ),
@( Default_Ignorable_Code_Point  DI        DerivedCoreProperties.txt     ),
@( Deprecated                    Dep       PropList.txt                  ),
@( Diacritic                     Dia       PropList.txt                  ),
@( Emoji                         -         emoji-data.txt                ),
@( Emoji_Component               EComp     emoji-data.txt                ),
@( Emoji_Modifier                EMod      emoji-data.txt                ),
@( Emoji_Modifier_Base           EBase     emoji-data.txt                ),
@( Emoji_Presentation            EPres     emoji-data.txt                ),
@( Extended_Pictographic         ExtPict   emoji-data.txt                ),
@( Extender                      Ext       PropList.txt                  ),
@( Grapheme_Base                 Gr_Base   DerivedCoreProperties.txt     ),
@( Grapheme_Extend               Gr_Ext    DerivedCoreProperties.txt     ),
@( Hex_Digit                     Hex       PropList.txt                  ),
@( IDS_Binary_Operator           IDSB      PropList.txt                  ),
@( IDS_Trinary_Operator          IDST      PropList.txt                  ),
@( ID_Continue                   IDC       DerivedCoreProperties.txt     ),
@( ID_Start                      IDS       DerivedCoreProperties.txt     ),
@( Ideographic                   Ideo      PropList.txt                  ),
@( Join_Control                  Join_C    PropList.txt                  ),
@( Logical_Order_Exception       LOE       PropList.txt                  ),
@( Lowercase                     Lower     DerivedCoreProperties.txt     ),
@( Math                          -         DerivedCoreProperties.txt     ),
@( Noncharacter_Code_Point       NChar     PropList.txt                  ),
@( Pattern_Syntax                Pat_Syn   PropList.txt                  ),
@( Pattern_White_Space           Pat_WS    PropList.txt                  ),
@( Quotation_Mark                QMark     PropList.txt                  ),
@( Radical                       -         PropList.txt                  ),
@( Regional_Indicator            RI        PropList.txt                  ),
@( Sentence_Terminal             STerm     PropList.txt                  ),
@( Soft_Dotted                   SD        PropList.txt                  ),
@( Terminal_Punctuation          Term      PropList.txt                  ),
@( Unified_Ideograph             UIdeo     PropList.txt                  ),
@( Uppercase                     Upper     DerivedCoreProperties.txt     ),
@( Variation_Selector            VS        PropList.txt                  ),
@( White_Space                   space     PropList.txt                  ),
@( XID_Continue                  XIDC      DerivedCoreProperties.txt     ),
@( XID_Start                     XIDS      DerivedCoreProperties.txt     ),
])

let internals = $create([  # for internally used property
@(Other_Alphabetic                    -  PropList.txt),
@(Other_Default_Ignorable_Code_Point  -  PropList.txt),
@(Prepended_Concatenation_Mark        -  PropList.txt),
@(Other_Grapheme_Extend               -  PropList.txt),
@(Other_ID_Continue                   -  PropList.txt),
@(Other_ID_Start                      -  PropList.txt),
@(Other_Lowercase                     -  PropList.txt),
@(Other_Math                          -  PropList.txt),
@(Other_Uppercase                     -  PropList.txt),
@(InCB_Linker                         -  DerivedCoreProperties.txt),    # for graphme cluster
@(InCB_Consonant                      -  DerivedCoreProperties.txt),    # for graphme cluster
@(InCB_Extend                         -  DerivedCoreProperties.txt),    # for graphme cluster
@(MidLetter                           -  WordBreakProperty.txt),
@(MidNumLet                           -  WordBreakProperty.txt),
@(Single_Quote                        -  WordBreakProperty.txt),
@(XID_Continue_Delta                  -  dummy), # dummy property for XID_Continue
@(XID_Start_Delta                     -  dummy), # dummy property for XID_Start
])


_main(param : _Param) {
    shctl set errraise

    $assertCombinedSet()

    ## generate lone property header
    {
        auto_gen_header
        echo '#define EACH_UCP_LONE_PROPERTY_NAME(E) \'
        echo '    /* for prime lone property */ \'
        for def in $defs {
            $def.query || continue
            var names = [$def.name]
            if let abbr = $def.abbr {
                $names.add($abbr)
            }
            echo "    E(${$def.name}, \"${$names.join('|')}\") \\"
        }
        echo '    /* for combined lone property */ \'
        for def in $defs {
            !$def.query || continue
            var names = [$def.name]
            if let abbr = $def.abbr {
                $names.add($abbr)
            }
            echo "    E(${$def.name}, \"${$names.join('|')}\") \\"
        }
        echo
        echo
        echo '#define EACH_UCP_LONE_PROPERTY_PRIME_INTERNAL(E) \'
        for def in $internals {
            assert $def.query
            echo "    E(${$def.name}) \\"
        }
        echo
        echo
        echo '#define EACH_UCP_LONE_PROPERTY_PRIME(E) \'
        for def in $defs {
            $def.query || continue
            echo "    E(${$def.name}) \\"
        }
        echo
        echo
        echo '#define EACH_UCP_LONE_PROPERTY(E) \'
        echo '    EACH_UCP_LONE_PROPERTY_PRIME_INTERNAL(E) \'
        echo '    EACH_UCP_LONE_PROPERTY_PRIME(E) \'
        for def in $defs {
            !$def.query || continue
            echo "    E(${$def.name}) \\"
        }
        echo '    /* end */'
        echo
        echo "#define UCP_LONE_PROPERTY_PRIME_INTERNAL_SIZE ${$internals.size()}"
        echo "#define UCP_LONE_PROPERTY_SIZE ${$internals.size()+$defs.size()}"
    } with > ${param.header}

    ## generate each lone property table
    {
        auto_gen_header

        var sizeMap: [String: OffsetEntry]
        var offset = 0
        echo "static constexpr PROPERTY_SET_RANGE lone_set_prime_table[] = {"
        echo "    /* for internal used property */"
        for def in $internals {
            var query = $def.query!
            var set = $createCodePointSet($query, $true)
            echo "    /* ${def.name} */"
            $set.emit()
            $sizeMap.put($def.name, $set.toOffsetEntry($offset))
            $offset += $set.tableSize()
        }
        echo "    /* for external used property */"
        for def in $defs {
            var query = $def.query ?? continue
            var set = $createCodePointSet($query, $true)
            echo "    /* ${def.name} */"
            $set.emit()
            $sizeMap.put($def.name, $set.toOffsetEntry($offset))
            $offset += $set.tableSize()
        }
        echo "};"

        ## generate final table
        echo
        echo "static constexpr PROPERTY_SET_RANGE_TABLE_OFFSET lone_set_prime_table_offset[] = {"
        for cate, s in $sizeMap {
            echo "    {${$s.str()}}, // $cate"
        }
        echo "};"
        echo
    } with > ${param.output}

    echo code generation success
}

shctl is-sourced || _main $@