#!/usr/bin/env arsh

source ./guniset_helper.arsh
source ./gen_lone_property_table.arsh

[<CLI(toplevel: $true)>]
type _Param() {
    [<Option(choice:['cpp', 'arsh'], default:"cpp", help:"output type (cpp, arsh), default is cpp")>]
    var type = ""

    [<Arg(help: "specify output")>]
    var output = "/dev/stdout"
}

function gen_for_arsh(output: String) {
    var targets : [String]
    for line in <(guniset enum 'sc') {
        $targets.add($line.split(", ")[1])
    }
    for target in $targets {
        echo -n '    '
        for line in <(guniset sample --limit=3 "sc:${target}") {
            var code = "\$'\\U${$line.slice('U+'.size())}'"
            echo -n "($code, '$target'), "
        }
        echo
    } with > $output
}

function sample(op: String): [String] {
    var r = "$(guniset sample --seed=42 --limit=10 $op)".replace("U+", "0x")
    return $r.empty() ? new [String]() : $r.split($'\n')
}

function gen_for_cpp(output: String) {
    {
        auto_gen_header
        echo "static constexpr PROPERTY_TEST_ENTRY property_test_table[] = {"
    } with > $output

    # general category
    var targets: [String]
    for line in <(guniset enum 'gc') {
        $targets.push($line.split(", ")[0])
    }
    for target in $targets {
        var ss = $sample("gc:$target")
        $ss.empty() && continue
        printf "    {\"gc\", \"%s\", {%s}},\n" $target ${$ss.join(", ")}
    } with >> $output

    # script
    $targets = new [String]()
    for line in <(guniset enum 'sc') {
        $targets.push($line.split(", ")[0])
    }
    for target in $targets {
        var ss = $sample("sc:$target")
        $ss.empty() && continue
        printf "    {\"sc\", \"%s\", {%s}},\n" $target ${$ss.join(", ")}
    } with >> $output

    # script extension
    $targets = new [String]()
    for line in <(guniset enum 'scx') {
        $targets.push($line.split(", ")[0])
    }
    for target in $targets {
        var ss = $sample("scx:$target")
        $ss.empty() && continue
        assert !$ss.join(", ").empty() : "<$ss>"
        printf "    {\"scx\", \"%s\", {%s}},\n" $target ${$ss.join(", ")}
    } with >> $output

    # lone property
    for def in $defs {
        let query = if let q = $def.query {
            $q
        } elif let p = $def.prefix {
            "$p:${def.name}"
        } elif $def.name == 'Assigned' {
            "!(cat:Cn)"
        } else {
            echo 1>&2 "[warn] ignore: ${def.name}"
            continue
        }
        var ss = $sample($query)
        assert !$ss.join(", ").empty() : "<$ss>"
        printf "    {\"\", \"%s\", {%s}},\n" ${def.name} ${$ss.join(", ")}
    } with >> $output

    echo "};" >> $output
}

_main(p : _Param) {
    case $p.type {
        'cpp' => $gen_for_cpp($p.output)
        'arsh' => $gen_for_arsh($p.output)
    }
    return 0
}

shctl is-sourced || _main $@