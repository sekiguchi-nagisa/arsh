#!/usr/bin/env arsh

if $# != 1 {
    echo "[usage] $0 [output]" 1>&2
    exit 1
}

command -v curl &> /dev/null || { echo require curl 1>&2; exit 1; }

let out = $1.startsWith("/") ? $1 : "../$1"
let work_dir = "emoji-seq"

test -d $work_dir || mkdir $work_dir

cd $work_dir

test -f ./emoji-sequences.txt ||
    curl https://www.unicode.org/Public/UCD/latest/emoji/emoji-sequences.txt \
        -o ./emoji-sequences.txt

test -f ./emoji-zwj-sequences.txt ||
    curl https://www.unicode.org/Public/UCD/latest/emoji/emoji-zwj-sequences.txt \
        -o ./emoji-zwj-sequences.txt


type Entry {
    let prop: String
    let codes: [Int]
}

function format(): String for Entry {
    var ret = "{EmojiProperty::${this.prop}, { "
    printf -v var "0x%04X, " ${this.codes}
    $ret += $reply['var']
    $ret += "}},"
    return $ret
}

function extract(filename: String, entries: [Entry]): [String] {
    var headers: [String]
    for line in new FD($filename) {
        $line.empty() && continue
        if $line.startsWith('#') {
            if $headers.size() < 2 {
                $headers.add($line)
            }
            continue
        }
 
        var ret = $/^(?<codes>[0-9A-F]+(( +[0-9A-F]+)+|\.\.[0-9A-F]+)?) *; *(?<prop>[a-zA-Z_]+) *;.+$/.match($line) ?? { echo failed at: $line; exit 1; }
        var codeStr = $ret.named('codes')!
        var prop = $ret.named('prop')!
        if $codeStr.contains('..') {
            var ss = $codeStr.split('..')
            var first = "0x${$ss[0]}".toInt()!
            var last = "0x${$ss[1]}".toInt()!
            for(; $first <= $last; $first++) {
                $entries.add(new Entry($prop, [$first]))
            }
        } else {
            var codes: [Int]
            for s in $codeStr.split(' ') {
                $codes.add("0x${s}".toInt()!)
            }
            $entries.add(new Entry($prop, $codes))
        }
    }
    return $headers
}

var entries: [Entry]
var headers : [String]

$headers.addAll($extract('./emoji-sequences.txt', $entries))
$headers.addAll($extract('./emoji-zwj-sequences.txt', $entries))

{
    echo "/* Auto-generated by scripts/unicode/$(basename $0) */"
    echo "/*"
    for h in $headers {
        echo $h
    }
    echo "*/"
    echo
    echo // clang-format off
    echo
    echo "static constexpr EMOJI_SEQ_ENTRY emoji_seq_table[] = {"
    for e in $entries {
        echo "    ${$e.format()}"
    }
    echo "};"
} with > $out